# Kernel: hgemm_nt_128x128

# Copyright 2014 Nervana Systems Inc. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

[-

our $int16;

our $convert = $int16 ? 'I2F.F32.S16' : 'F2F.F32.F16';

sub convert_in {return $convert;}


sub int16_params {
    return $int16 ? q{
param_Stats[0]  : c[0x0][0x190]
param_Stats[1]  : c[0x0][0x194]
param_scale     : c[0x0][0x198]
    } : "";
}
-]


<CONSTANT_MAPPING>
    addr_zero  : 4x<128*8*4>

    gridDimA : c[0x0][0x14]
    gridDimB : c[0x0][0x18]

    param_C[0]      : c[0x0][0x140]
    param_C[1]      : c[0x0][0x144]
    param_A[0]      : c[0x0][0x148]
    param_A[1]      : c[0x0][0x14c]
    param_B[0]      : c[0x0][0x150]
    param_B[1]      : c[0x0][0x154]
    param_alpha     : c[0x0][0x158]
    param_beta      : c[0x0][0x15c]
    param_flags     : c[0x0][0x160]
    param_lda       : c[0x0][0x164]
    param_ldb       : c[0x0][0x168]
    param_ldc       : c[0x0][0x16c]
    param_m         : c[0x0][0x170]
    param_n         : c[0x0][0x174]
    param_k         : c[0x0][0x178]
    param_ldaz      : c[0x0][0x17c]
    param_ldbz      : c[0x0][0x180]
    param_ldcz      : c[0x0][0x184]
    param_loops     : c[0x0][0x188]
    [+ int16_params() +]
</CONSTANT_MAPPING>

<REGISTER_MAPPING>

    64-95   ~ tidX, blk, lda, ldb, ldaz, ldbz, tid1, tid7, tid128, tid127, txa, txb, xmad_ta, xmad_tb, k1, k2, k3

    0-63    : czero<00-63>

     3, 2,11,10,19,18,27,26 : cx<0-7>y0
     7, 6,15,14,23,22,31,30 : cx<0-7>y1
     1, 0, 9, 8,17,16,25,24 : cx<0-7>y2
     5, 4,13,12,21,20,29,28 : cx<0-7>y3
    35,34,43,42,51,50,59,58 : cx<0-7>y4
    39,38,47,46,55,54,63,62 : cx<0-7>y5
    33,32,41,40,49,48,57,56 : cx<0-7>y6
    37,36,45,44,53,52,61,60 : cx<0-7>y7

    64-79   : j0Ay<0-7>, j0Bx<0-7>
    80-95   : j1Ay<0-7>, j1Bx<0-7>

    96-107  : loadA<0-5>, loadB<0-5>

    108-111 : trackA<0-1>, trackB<0-1>

    112-118 ~ writeS, k, tidY, ta, tb, loop
    119-127 ~ readAs, readBs, tid, blkA, blkB, blkZ

    64-75   ~ ldc, ldcz, ci, xmad_c, tid_31, tid_96, tid_128

    64-79   : c<0-7>, d3, d2, d1, d0, cs<0-3>
    64-65   : Stats<0-1>
    80-89   : C00y<0-1>, C04y<0-1>, C08y<0-1>, C12y<0-1>
    90-118  ~ ldc1, ldc4, ldc60, writeCs, readCs, cx<00|64>, cy<00|04|08|12>, alpha, beta, flags, warp_max, maxabs

</REGISTER_MAPPING>

--:-:1:-:1      S2R tid,  SR_TID.X;
--:-:2:-:1      S2R blkA, SR_CTAID.Y;
--:-:3:-:1      S2R blkB, SR_CTAID.Z;
--:-:4:-:1      S2R blkZ, SR_CTAID.X;

<SCHEDULE_BLOCK>
--:-:-:-:1      MOV k,  param_k;
--:-:-:-:1      LOP.AND tid1, tid,  1;
--:-:-:-:1      LOP.AND tid128, tid,  128;
--:-:-:-:1      MOV loop, RZ;

--:-:-:-:1      STS.128 [addr_zero], RZ;
[+
        join('', map sprintf("--:-:-:-:1      LDS.U.128 czero%02d, [addr_zero];\n", $_ * 4), 0..15);
+]

--:-:-:-:1      MOV lda, param_lda;
--:-:-:-:1      MOV ldb, param_ldb;
--:-:-:-:1      MOV ldaz, param_ldaz;
--:-:-:-:1      MOV ldbz, param_ldbz;

// tidY = tid1 << 2
--:-:-:-:1      SHL tidY, tid1, 2;

// tidX = tid >> 1
01:-:-:-:1      SHR.U32 tidX,  tid,   1;

// trackA += 2 * ((blkA*128 + tidX) * lda + tidY)
02:-:-:-:1      ISCADD  txa, blkA, tidX, 7;
--:-:-:-:1      XMAD.LO  ta, lda,  txa,  tidY, xmad_ta;
08:-:-:-:1      XMAD.LO2 ta, ldaz, blkZ, ta;
--:-:-:-:1      LEA      trackA0.CC, ta, param_A[0],     0x1;
--:-:-:-:1      LEA.HI.X trackA1,    ta, param_A[1], RZ, 0x1;

// trackB += 2 * ((blkB*128 + tidX) * ldb + tidY)
04:-:-:-:1      ISCADD   txb, blkB, tidX, 7;
--:-:-:-:1      XMAD.LO  tb,  ldb,  txb,  tidY, xmad_tb;
08:-:-:-:1      XMAD.LO2 tb,  ldbz, blkZ, tb;
--:-:-:-:1      LEA      trackB0.CC, tb, param_B[0],     0x1;
--:-:-:-:1      LEA.HI.X trackB1,    tb, param_B[1], RZ, 0x1;

--:-:-:-:1      ISETP.LT.AND P5, PT, txa, param_m, PT;
--:-:-:-:1      ISETP.LT.AND P6, PT, txb, param_n, PT;

// writeS = 4 * (128 * tidY + tidX)
--:-:-:-:1      ISCADD  writeS, tidY, tidX, 7;
--:-:-:-:1      ISCADD  writeS, writeS, 4x<128*8*2>, 2;

// readAs  = (((tid & 0x70) >> 3) | (tid & 1)) << 4
--:-:-:-:1      LOP.AND readAs, tid,    0x70;
--:-:-:-:1      SHR.U32 readAs, readAs, 3;
--:-:-:-:1      LOP.OR  readAs, readAs, tid1;
--:-:-:-:1      SHL     readAs, readAs, 4;

// readBs = ((tid128 >> 4) | ((tid >> 1) & 7)) << 4 + 4096;
--:-:-:-:1      BFE.U32 tid7,   tid,    0x301; // 3 bits at position 1
--:-:-:-:1      SHR.U32 readBs, tid128, 4;
--:-:-:-:1      LOP.OR  readBs, readBs, tid7;
--:-:-:-:1      ISCADD  readBs, readBs, 4x<128*8>, 4;

</SCHEDULE_BLOCK>

REMAINDER:

[+
    our $vec;
    return $vec ? q{
// k must be multiple of 8
--:-:-:-:0      PSETP.AND.AND P1, PT, PT, PT, PT;

--:-:2:-:1  @P5 LDG.E.CI.64 loadA0, [trackA + 2x<0>];
--:-:2:-:1  @P5 LDG.E.CI.64 loadA4, [trackA + 2x<8>];
--:-:4:-:1  @P6 LDG.E.CI.64 loadB0, [trackB + 2x<0>];
--:5:6:-:1  @P6 LDG.E.CI.64 loadB4, [trackB + 2x<8>];

--:-:3:-:1 @!P5 LDS.U.64    loadA0, [addr_zero];
--:-:3:-:1 @!P5 LDS.U.64    loadA4, [addr_zero];
--:-:3:-:1 @!P6 LDS.U.64    loadB0, [addr_zero];
--:-:3:-:2 @!P6 LDS.U.64    loadB4, [addr_zero];

    // Vec 4 and scalar loads
    } : q{
<SCHEDULE_BLOCK>
--:-:-:-:1      IADD k1, tidY, 1;
--:-:-:-:1      IADD k2, tidY, 2;
--:-:-:-:1      IADD k3, tidY, 3;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidY, k, P5;
--:-:-:-:1      ISETP.LT.AND P1, PT, k1, k, P5;
--:-:-:-:1      ISETP.LT.AND P2, PT, k2, k, P5;
--:-:-:-:1      ISETP.LT.AND P3, PT, k3, k, P5;

--:-:2:-:1  @P0 LDG.E.CI.S16 loadA0, [trackA + 2x<0>];
--:-:2:-:1  @P1 LDG.E.CI.S16 loadA1, [trackA + 2x<1>];
--:-:2:-:1  @P2 LDG.E.CI.S16 loadA2, [trackA + 2x<2>];
--:-:2:-:1  @P3 LDG.E.CI.S16 loadA3, [trackA + 2x<3>];

--:-:-:-:1 @!P0 MOV loadA0, RZ;
--:-:-:-:1 @!P1 MOV loadA1, RZ;
--:-:-:-:1 @!P2 MOV loadA2, RZ;
--:-:-:-:1 @!P3 MOV loadA3, RZ;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidY, k, P6;
--:-:-:-:1      ISETP.LT.AND P1, PT, k1, k, P6;
--:-:-:-:1      ISETP.LT.AND P2, PT, k2, k, P6;
--:-:-:-:1      ISETP.LT.AND P3, PT, k3, k, P6;

--:-:4:-:1  @P0 LDG.E.CI.S16 loadB0, [trackB + 2x<0>];
--:-:4:-:1  @P1 LDG.E.CI.S16 loadB1, [trackB + 2x<1>];
--:-:4:-:1  @P2 LDG.E.CI.S16 loadB2, [trackB + 2x<2>];
--:-:4:-:1  @P3 LDG.E.CI.S16 loadB3, [trackB + 2x<3>];

--:-:-:-:1 @!P0 MOV loadB0, RZ;
--:-:-:-:1 @!P1 MOV loadB1, RZ;
--:-:-:-:1 @!P2 MOV loadB2, RZ;
--:-:-:-:1 @!P3 MOV loadB3, RZ;

// bDoRemainder = k & 7 && k > 8
--:-:-:-:1      LOP.AND.NZ P4, RZ, k, 7;
--:-:-:-:1      ISETP.GT.AND P1, PT, k, 8, P4;
</SCHEDULE_BLOCK>
    };
+]

[+
    our $vec;
    our $convert;
    return $vec ? qq{

06:-:1:-:4      $convert loadA3, loadA1.H1;
--:-:-:-:0      IADD   trackA0.CC, trackA0, 2x<16>;
--:-:2:-:4      $convert loadA2, loadA1.H0;
--:-:-:-:4      $convert loadA1, loadA0.H1;
--:-:-:-:0      IADD.X trackA1, trackA1, RZ;
--:-:3:-:1      $convert loadA0, loadA0.H0;

01:-:-:-:1      STS [writeS + 4x<3*128>], loadA3;
02:-:-:-:1      STS [writeS + 4x<2*128>], loadA2;
04:-:-:-:1      STS [writeS + 4x<1*128>], loadA1;
--:-:-:-:1      STS [writeS + 4x<0*128>], loadA0;

08:-:1:-:4      $convert loadB3, loadB1.H1;
10:-:-:-:0      IADD   trackB0.CC, trackB0, 2x<16>;
--:-:2:-:4      $convert loadB2, loadB1.H0;
--:-:3:-:4      $convert loadB1, loadB0.H1;
--:-:-:-:0      IADD.X trackB1, trackB1, RZ;
--:-:4:-:1      $convert loadB0, loadB0.H0;

01:-:-:-:1      STS [writeS + 4x<11*128>], loadB3;
02:-:-:-:1      STS [writeS + 4x<10*128>], loadB2;
04:-:-:-:1      STS [writeS + 4x< 9*128>], loadB1;
08:-:-:-:1      STS [writeS + 4x< 8*128>], loadB0;

    // scalar loads
    } : qq{

02:-:-:-:4      $convert loadA0, loadA0;
--:-:-:-:0      IADD   trackA0.CC, trackA0, 2x<8>;
--:-:2:-:4      $convert loadA1, loadA1;
--:-:-:-:4      $convert loadA2, loadA2;
--:-:3:-:1      $convert loadA3, loadA3;

--:-:-:-:0      IADD.X trackA1, trackA1, RZ;

02:-:-:-:1      STS [writeS + 4x<0*128>], loadA0;
--:-:-:-:1      STS [writeS + 4x<1*128>], loadA1;
04:-:-:-:1      STS [writeS + 4x<2*128>], loadA2;
--:-:-:-:1      STS [writeS + 4x<3*128>], loadA3;

08:-:-:-:4      $convert loadB0, loadB0;
--:-:-:-:0      IADD   trackB0.CC, trackB0, 2x<8>;
--:-:2:-:4      $convert loadB1, loadB1;
--:-:-:-:4      $convert loadB2, loadB2;
--:-:3:-:1      $convert loadB3, loadB3;

--:-:-:-:0      IADD.X trackB1, trackB1, RZ;

02:-:-:-:1      STS [writeS + 4x< 8*128>], loadB0;
--:-:-:-:1      STS [writeS + 4x< 9*128>], loadB1;
04:-:-:-:1      STS [writeS + 4x<10*128>], loadB2;
--:-:-:-:1      STS [writeS + 4x<11*128>], loadB3;
    };
+]


--:-:-:-:1      LOP.XOR readAs, readAs, 4x<128*8*2>;
--:-:-:-:0      LOP.XOR readBs, readBs, 4x<128*8*2>;
01:-:-:-:5      BAR.SYNC 0;
--:-:-:-:0      LOP.XOR writeS, writeS, 4x<128*8*2>;


[+
    our $vec;
    our $convert;
    our @top = $vec ?
        ("--:-:-:-:1      ISETP.GE.AND P0, PT, k, 16, PT;\n") :
        ("--:-:-:-:1      ISETP.GE.AND P2, PT, k, 16, P5;\n");
    our %insert =
    (
        ($vec ?
            (
        j0c1  => "--:-:-:-:1      PSETP.AND.AND P1, PT, !P1, PT, PT;\n",
        j0c13 => "--:-:-:-:1      PSETP.AND.AND P2, PT, P0, P1, P5;\n",
        j0c15 => "--:-:-:-:1      PSETP.AND.AND P3, PT, P0, P1, P6;\n",

        j0c27 => "--:-:2:-:1  \@P2 LDG.E.CI.64 loadA0, [trackA + 2x<0>];\n",
        j0c29 => "--:-:2:-:1  \@P2 LDG.E.CI.64 loadA4, [trackA + 2x<8>];\n",
        j0c31 => "--:-:4:-:1  \@P3 LDG.E.CI.64 loadB0, [trackB + 2x<0>];\n",
        j0c33 => "20:5:6:-:1  \@P3 LDG.E.CI.64 loadB4, [trackB + 2x<8>];\n",

        j3c5  => "--:-:-:-:1 \@!P1 $convert loadA3, loadA5.H1;\n",
        j3c9  => "--:-:-:-:1 \@!P1 $convert loadA2, loadA5.H0;\n",
        j3c13 => "--:-:-:-:1 \@!P1 $convert loadA1, loadA4.H1;\n",
        j3c17 => "--:-:-:-:1 \@!P1 $convert loadA0, loadA4.H0;\n",

        j4c5  => "--:-:-:-:1 \@!P1 $convert loadB3, loadB5.H1;\n",
        j4c9  => "--:-:-:-:1 \@!P1 $convert loadB2, loadB5.H0;\n",
        j4c13 => "--:-:-:-:1 \@!P1 $convert loadB1, loadB4.H1;\n",
        j4c17 => "--:-:-:-:1 \@!P1 $convert loadB0, loadB4.H0;\n",

        j5c5  => "02:-:-:-:1  \@P1 $convert loadA3, loadA1.H1;\n",
        j5c9  => "--:-:2:-:1  \@P1 $convert loadA2, loadA1.H0;\n",
        j5c13 => "--:-:-:-:1  \@P1 $convert loadA1, loadA0.H1;\n",
        j5c17 => "--:-:3:-:1  \@P1 $convert loadA0, loadA0.H0;\n",

        j5c29 => "02:-:-:-:1  \@P0 STS [writeS + 4x<3*128>], loadA3;\n",
        j5c31 => "--:-:-:-:1  \@P0 STS [writeS + 4x<2*128>], loadA2;\n",
        j5c33 => "04:-:-:-:1  \@P0 STS [writeS + 4x<1*128>], loadA1;\n",
        j5c35 => "--:-:-:-:1  \@P0 STS [writeS + 4x<0*128>], loadA0;\n",

        j6c5  => "08:-:-:-:1  \@P1 $convert loadB3, loadB1.H1;\n",
        j6c9  => "--:-:2:-:1  \@P1 $convert loadB2, loadB1.H0;\n",
        j6c13 => "--:-:3:-:1  \@P1 $convert loadB1, loadB0.H1;\n",
        j6c17 => "--:-:4:-:1  \@P1 $convert loadB0, loadB0.H0;\n",

        j6c29 => "02:-:-:-:1  \@P0 STS [writeS + 4x<11*128>], loadB3;\n",
        j6c31 => "--:-:-:-:1  \@P0 STS [writeS + 4x<10*128>], loadB2;\n",
        j6c33 => "04:-:-:-:1  \@P0 STS [writeS + 4x< 9*128>], loadB1;\n",
        j6c35 => "08:-:-:-:1  \@P0 STS [writeS + 4x< 8*128>], loadB0;\n",

        j5c46 => "--:-:-:-:1  \@P2 IADD   trackA0.CC, trackA0, 2x<16>;\n",
        j5c54 => "--:-:-:-:1  \@P2 IADD.X trackA1,    trackA1, RZ;\n",

        j6c46 => "10:-:-:-:1  \@P3 IADD   trackB0.CC, trackB0, 2x<16>;\n",
        j6c54 => "--:-:-:-:1  \@P3 IADD.X trackB1,    trackB1, RZ;\n",

        j7c63 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n",
            ) :
            (
        j0c7  => "--:-:-:-:1      ISETP.GE.AND P3, PT, k, 16, P6;\n",
        j0c8  => "--:-:-:-:1      ISETP.GE.AND P0, PT, k, 16, PT;\n",

        j0c10 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA0, [trackA + 2x<0>];\n",
        j0c12 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA1, [trackA + 2x<1>];\n",
        j0c14 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA2, [trackA + 2x<2>];\n",
        j0c16 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA3, [trackA + 2x<3>];\n",

        j0c29 => "--:-:6:-:1  \@P3 LDG.E.CI.S16 loadB0, [trackB + 2x<0>];\n",
        j0c31 => "--:-:6:-:1  \@P3 LDG.E.CI.S16 loadB1, [trackB + 2x<1>];\n",
        j0c33 => "--:-:6:-:1  \@P3 LDG.E.CI.S16 loadB2, [trackB + 2x<2>];\n",
        j0c35 => "--:-:6:-:1  \@P3 LDG.E.CI.S16 loadB3, [trackB + 2x<3>];\n",

        j5c5  => "02:-:2:-:1  \@P2 $convert loadA0, loadA0;\n",
        j5c9  => "--:-:3:-:1  \@P2 $convert loadA1, loadA1;\n",
        j5c13 => "--:-:4:-:1  \@P2 $convert loadA2, loadA2;\n",
        j5c17 => "--:-:5:-:1  \@P2 $convert loadA3, loadA3;\n",

        j5c29 => "02:-:-:-:1  \@P0 STS [writeS + 4x<0*128>], loadA0;\n",
        j5c31 => "04:-:-:-:1  \@P0 STS [writeS + 4x<1*128>], loadA1;\n",
        j5c33 => "08:-:-:-:1  \@P0 STS [writeS + 4x<2*128>], loadA2;\n",
        j5c35 => "10:-:-:-:1  \@P0 STS [writeS + 4x<3*128>], loadA3;\n",

        j6c5  => "20:-:2:-:1  \@P3 $convert loadB0, loadB0;\n",
        j6c9  => "--:-:3:-:1  \@P3 $convert loadB1, loadB1;\n",
        j6c13 => "--:-:4:-:1  \@P3 $convert loadB2, loadB2;\n",
        j6c17 => "--:-:5:-:1  \@P3 $convert loadB3, loadB3;\n",

        j6c29 => "02:-:-:-:1  \@P0 STS [writeS + 4x< 8*128>], loadB0;\n",
        j6c31 => "04:-:-:-:1  \@P0 STS [writeS + 4x< 9*128>], loadB1;\n",
        j6c33 => "08:-:-:-:1  \@P0 STS [writeS + 4x<10*128>], loadB2;\n",
        j6c35 => "10:-:-:-:1  \@P0 STS [writeS + 4x<11*128>], loadB3;\n",

        j5c46 => "--:-:-:-:1  \@P2 IADD   trackA0.CC, trackA0, 2x<8>;\n",
        j5c54 => "--:-:-:-:1  \@P2 IADD.X trackA1,    trackA1, RZ;\n",

        j6c46 => "10:-:-:-:1  \@P3 IADD   trackB0.CC, trackB0, 2x<8>;\n",
        j6c54 => "--:-:-:-:1  \@P3 IADD.X trackB1,    trackB1, RZ;\n",

        j7c63 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n" .
                 "--:-:-:Y:5  \@P1 BRA.U REMAINDER;\n",
            )
        ),

        j6c63 => "--:-:-:-:5  \@P0 BAR.SYNC 0;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR readAs, readAs, 4x<128*8*2>;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR readBs, readBs, 4x<128*8*2>;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR writeS, writeS, 4x<128*8*2>;\n" .
                 "--:-:-:-:1      IADD32I k, k, -8;\n",
    );
    return;
+]

<INCLUDE file="hgemm_common_128x128.sass"/>
